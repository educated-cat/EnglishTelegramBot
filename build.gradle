plugins {
	id("io.spring.dependency-management").version("1.0.12.RELEASE")
}

subprojects {
	group = "com.educatedcat.englishtelegrambot"
	version = "0.0.1-SNAPSHOT"
	
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java-library'
	
	repositories {
		mavenCentral()
	}
	
	configurations.all {
		exclude group: "org.apache.logging.log4j"
		exclude group: "org.skyscreamer"
	}
	
	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:2.7.5")
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.4"
			mavenBom("org.testcontainers:testcontainers-bom:1.17.5")
		}
	}
	
	dependencies {
		constraints {
			implementation("org.telegram:telegrambots-meta:6.1.0")
			implementation("io.jsonwebtoken:jjwt-impl:0.11.5")
		}
		
		/* Spring Boot*/
		implementation("org.springframework.boot:spring-boot-devtools")
		annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
		
		/* Server */
		implementation("io.netty:netty-resolver-dns-native-macos:4.1.79.Final:osx-aarch_64")
		
		/* Utilities */
		compileOnly("org.projectlombok:lombok")
		annotationProcessor("org.projectlombok:lombok")
		
		/* Test */
		testCompileOnly("org.projectlombok:lombok")
		testAnnotationProcessor("org.projectlombok:lombok")
	}
	
	compileJava {
		sourceCompatibility = 17
		targetCompatibility = 17
	}
	
	test {
		testLogging {
			events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
		}
	}
	
	tasks.test {
		useJUnitPlatform()
	}
}
